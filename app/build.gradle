apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("private/keystore.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 23
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "org.beiwe.app"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 20       //TODO: update this Beiwe version code for new releases
        versionName '2.2.1'  //TODO: update this Beiwe version number for new releases
        setProperty("archivesBaseName", "Beiwe-$versionName")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.config
            resValue "string", "app_name", "Beiwe"
            buildConfigField("boolean", "APP_IS_BETA", "false")
            buildConfigField("boolean", "APP_IS_DEV", "false")
        }
        beta {
            versionNameSuffix("-beta")
            signingConfig signingConfigs.config
            resValue "string", "app_name", "Beiwe-beta"
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "false")
        }
        development {
            versionNameSuffix("-development")
            signingConfig signingConfigs.config
            resValue "string", "app_name", "Beiwe-development"
            buildConfigField("boolean", "APP_IS_BETA", "true")
            buildConfigField("boolean", "APP_IS_DEV", "true")
        }
    }
    useLibrary "org.apache.http.legacy"
}
// Don't include the default "debug" build variant as an option
android.variantFilter { variant ->
    if(variant.buildType.name.equals('debug')) {
        variant.setIgnore(true);
    }
}

repositories {
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:19.1.0'
    compile 'com.commonsware.cwac:anddown:0.3.0'
}
